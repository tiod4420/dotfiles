" Vim configuration

function! CheckAndSource(file_path)
	if filereadable(a:file_path)
		execute "source " a:file_path
	endif
endfunction

" Get path of config directory
let config_dir_path=fnamemodify(expand($MYVIMRC), ':p:h') . '/config'

" For Windows version only
if has('win32') || has('win64')
	" Force 256 colors
	set t_Co=256

	" Remove Windows style path
	set runtimepath-=~/vimfiles
	set runtimepath-=~/vimfiles/after
	set packpath-=~/vimfiles
	set packpath-=~/vimfiles/after

	set runtimepath^=~/.vim
	set runtimepath+=~/.vim/after
	set packpath^=~/.vim
	set packpath+=~/.vim/after
endif

" Fix tmux term capabilities
if &term =~ "^tmux"
	if &t_ST == "" | let &t_ST = "\e[22;2t" | endif
	if &t_RT == "" | let &t_RT = "\e[23;2t" | endif
elseif &term =~ "^alacritty"
	set ttymouse=sgr
endif

" Source all the configuration files
for file in [ "global.vim", "theme.vim", "functions.vim", "filetypes.vim", "mappings.vim" ]
	call CheckAndSource(config_dir_path . "/" . file)
endfor
unlet file

" Load local configuration files that are not commited
" [!] Load after all other settings so it can override previous config
for file in split(expand(config_dir_path . "/local/*.vim"), "\n")
	call CheckAndSource(file)
endfor

unlet config_dir_path
delfunction CheckAndSource
